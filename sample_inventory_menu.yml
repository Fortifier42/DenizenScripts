# |----------------------
# |
# | Sample Inventory Menu
# |
#
# @author mcmonkey
# @date 2014 / 10 / 19
# @build 1533
# @version 1.0
#
# Installation:
# To test this, simple put the script in your folder
# and reload scripts. Do /sampleinvmenu to open the menu.
# Note: You need permission denizen.sampleinvmenu to run the command.
# Note: You need permission denizen.sampleinvmenu.god to use the 'god' item.
# This is meant to be used as a basis for your own inventory menus,
# not used directly.
#
#
# ---------------------------- END HEADER ----------------------------

# The command handler
sim_handler_command:
    type: command
    name: sampleinvmenu
    description: Opens a sample inventory-menu.
    usage: /sampleinvmenu
    allowed help:
    - determine <player.has_permission[denizen.sampleinvmenu]>
    script:
    - if <context.server> {
      - narrate "<&c>This is a player-only command."
      - queue clear
      }
    - if !<player.has_permission[denizen.sampleinvmenu]> {
      - narrate "<&c>You lack the permission for this command."
      - queue clear
      }
    - inventory open d:in@sim_inventory

# The world event based handler
sim_handler_world:
    type: world
    events:
        on player clicks sim_god_item in inventory:
        - if !<player.has_permission[denizen.sampleinvmenu.god]> {
          - narrate "<&c>You lack the permission for this menu item."
          - queue clear
          }
        - narrate "<&2>At this point, you'd receive god!"
        - determine cancelled
        on player clicks sim_potato_item in inventory:
        - narrate "<&2>Here's a potato."
        - determine passively cancelled
        - wait 1t
        - give potato


# The main inventory script
sim_inventory:
    type: inventory
    inventory: chest
    title: <&b><&l>Sample Inventory Menu
    size: 27
    slots:
    - "[] [] [] [] [sim_god_item] [] [] [] []"
    - "[] [] [] [] [sim_potato_item] [] [] [] []"
    - "[] [] [] [] [] [] [] [] []"

# Here and below: the items for the inventory
sim_god_item:
    type: item
    material: diamond_block
    display name: <&b>God
    lore:
    - <&e>Gives you godmode.

sim_potato_item:
    type: item
    material: potato_item
    display name: <&b>Potato
    lore:
    - <&e>Gives you a potato.










